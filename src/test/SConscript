# -*- mode: python -*-
Import('env')

e = env.Clone(LIBPATH='#egen')
conf = Configure(e)

for hdr in ('boost/test/unit_test.hpp', 'boost/test/unit_test_log.hpp',
            'boost/test/unit_test_monitor.hpp',
            'boost/test/impl/unit_test_main.ipp',
            'boost/test/impl/framework.ipp'):
    if not conf.CheckCXXHeader(hdr):
        print "We need C++ header %s." % hdr
        Exit(1)

if not conf.CheckLib("boost_unit_test_framework-mt", language="C++") and \
        not conf.CheckLib("boost_unit_test_framework", language="C++"):
    print "We need C++ library boost_unit_test_framework(-mt)."
    Exit(1)

e = conf.Finish()

testprog = e.Program('libegen-test',
                     Split("""main.cpp TestConcurrentQueue.cpp
                              TestCoordinate.cpp TestEquality.cpp
                              TestInvalid.cpp TestMut.cpp TestMapLookup.cpp
                              TestPoint.cpp"""))
e.AddPostAction(testprog, 'LD_LIBRARY_PATH=egen test/libegen-test')

env.Alias("build", testprog)
